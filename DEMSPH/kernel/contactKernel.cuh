#pragma once
#include "myUtility/myVec.h"

__device__ __forceinline__ int ParallelBondedContact(double& bondNormalForce, 
double& bondTorsionalTorque, 
double3& bondShearForce, 
double3& bondBendingTorque,
double& maxNormalStress,
double& maxShearStress,
const double3 contactNormalPrev,
const double3 contactNormal,
const double3 relativeVelocityAtContact,
const double3 angularVelocityA,
const double3 angularVelocityB,
const double radiusA,
const double radiusB,
const double timeStep,
const double bondMultiplier,
const double bondElasticModulus,
const double bondStiffnessRatioNormalToShear,
const double bondTensileStrength,
const double bondCohesion,
const double bondFrictionCoefficient)
{
	const double3 theta1 = cross(contactNormalPrev, contactNormal);
	bondShearForce = rotateVector(bondShearForce, theta1);
	bondBendingTorque = rotateVector(bondBendingTorque, theta1);
	const double3 theta2 = dot(0.5 * (angularVelocityA + angularVelocityB) * timeStep, contactNormal) * contactNormal;
	bondShearForce = rotateVector(bondShearForce, theta2);
	bondBendingTorque = rotateVector(bondBendingTorque, theta2);
	const double minRadius = radiusA < radiusB ? radiusA : radiusB;
	const double bondRadius = bondMultiplier * minRadius;
	const double bondArea = bondRadius * bondRadius * pi();// cross-section area of beam of the bond
	const double bondInertiaMoment = bondRadius * bondRadius * bondRadius * bondRadius / 4. * pi();// inertia moment
	const double bondPolarInertiaMoment = 2 * bondInertiaMoment;// polar inertia moment
	const double normalStiffnessUnitArea = bondElasticModulus / (radiusA + radiusB);
	const double shearStiffnessUnitArea = normalStiffnessUnitArea / bondStiffnessRatioNormalToShear;

	const double3 normalTranslationIncrement = dot(relativeVelocityAtContact, contactNormal) * contactNormal * timeStep;
	const double3 tangentialTranslationIncrement = relativeVelocityAtContact * timeStep - normalTranslationIncrement;
	bondNormalForce -= dot(normalTranslationIncrement * normalStiffnessUnitArea * bondArea, contactNormal);
	bondShearForce -= tangentialTranslationIncrement * shearStiffnessUnitArea * bondArea;
	const double3 relativeAngularVelocity = angularVelocityA - angularVelocityB;
	const double3 normalRotationIncrement = dot(relativeAngularVelocity, contactNormal) * contactNormal * timeStep;
	const double3 tangentialRotationIncrement = relativeAngularVelocity * timeStep - normalRotationIncrement;
	bondTorsionalTorque -= dot(normalRotationIncrement * shearStiffnessUnitArea * bondPolarInertiaMoment, contactNormal);
	bondBendingTorque -= tangentialRotationIncrement * normalStiffnessUnitArea * bondInertiaMoment;

	maxNormalStress = -bondNormalForce / bondArea + length(bondBendingTorque) / bondInertiaMoment * bondRadius;// maximum tension stress
	maxShearStress = length(bondShearForce) / bondArea + fabs(bondTorsionalTorque) / bondPolarInertiaMoment * bondRadius;// maximum shear stress

	int isBonded = 1;
	if (bondTensileStrength > 0 && maxNormalStress > bondTensileStrength)
	{
		isBonded = 0;
	}
	else if (bondCohesion > 0 && maxShearStress > bondCohesion - bondFrictionCoefficient * maxNormalStress)
	{
		isBonded = 0;
	}
	return isBonded;
}

static __device__ __forceinline__ double3 integrateSlidingOrRollingSpring(const double3 springPrev, 
const double3 springVelocity, 
const double3 contactNormal, 
const double3 normalContactForce, 
const double frictionCoefficient, 
const double stiffness, 
const double dampingCoefficient, 
const double timeStep)
{
	double3 spring = make_double3(0., 0., 0.);
	if (stiffness > 0.)
	{
		double3 springPrev1 = springPrev - dot(springPrev, contactNormal) * contactNormal;
		double absoluteSpringPrev1 = length(springPrev1);
		if (absoluteSpringPrev1 > 1.e-10)
		{
			springPrev1 *= length(springPrev) / absoluteSpringPrev1;
		}
		spring = springPrev1 + springVelocity * timeStep;
		if (frictionCoefficient > 0.)
		{
			double3 springForce = -stiffness * spring - dampingCoefficient * springVelocity;
			double absoluteSpringForce = length(springForce);
			double absoluteNormalContactForce = length(normalContactForce);
			if (absoluteSpringForce > frictionCoefficient * absoluteNormalContactForce)
			{
				double ratio = frictionCoefficient * absoluteNormalContactForce / absoluteSpringForce;
				springForce *= ratio;
				spring = -(springForce + dampingCoefficient * springVelocity) / stiffness;
			}
		}
	}
	return spring;
}

static __device__ __forceinline__ double3 integrateTorsionSpring(const double3 springPrev, 
const double3 torsionRelativeVelocity, 
const double3 contactNormal, 
const double3 normalContactForce, 
const double frictionCoefficient, 
const double stiffness, 
const double dampingCoefficient, 
const double timeStep)
{
	double3 spring = make_double3(0., 0., 0.);
	if (stiffness > 0.)
	{
		spring = dot(springPrev + torsionRelativeVelocity * timeStep, contactNormal) * contactNormal;
		if (frictionCoefficient > 0.)
		{
			double3 springForce = -stiffness * spring - dampingCoefficient * torsionRelativeVelocity;
			double absoluteSpringForce = length(springForce);
			double absoluteNormalContactForce = length(normalContactForce);
			if (absoluteSpringForce > frictionCoefficient * absoluteNormalContactForce)
			{
				double ratio = frictionCoefficient * absoluteNormalContactForce / absoluteSpringForce;
				springForce *= ratio;
				spring = -(springForce + dampingCoefficient * torsionRelativeVelocity) / stiffness;
			}
		}
	}
	return spring;
}

__device__ __forceinline__ void LinearContact(double3& contactForce, 
double3& contactTorque, 
double3& slidingSpring, 
double3& rollingSpring, 
double3& torsionSpring,
const double3 relativeVelocityAtContact,
const double3 relativeAngularVelocityAtContact,
const double3 contactNormal,
const double normalOverlap,
const double effectiveMass,
const double effectiveRadius,
const double timeStep,
const double normalStiffness,
const double slidingStiffness,
const double rollingStiffness,
const double torsionStiffness,
const double normalDissipation,
const double slidingDissipation,
const double rollingDissipation,
const double torsionDissipation,
const double slidingFrictionCoefficient,
const double rollingFrictionCoefficient,
const double torsionFrictionCoefficient)
{
	if (normalOverlap > 0)
	{
		const double normalDampingCoefficient = 2. * normalDissipation * sqrt(effectiveMass * normalStiffness);
		const double slidingDampingCoefficient = 2. * slidingDissipation * sqrt(effectiveMass * slidingStiffness);
		const double rollingDampingCoefficient = 2. * rollingDissipation * sqrt(effectiveMass * rollingStiffness);
		const double torsionDampingCoefficient = 2. * torsionDissipation * sqrt(effectiveMass * torsionStiffness);

		const double3 normalRelativeVelocityAtContact = dot(relativeVelocityAtContact, contactNormal) * contactNormal;
		const double3 normalContactForce = normalStiffness * normalOverlap * contactNormal - normalRelativeVelocityAtContact * normalDampingCoefficient;

		const double3 slidingRelativeVelocity = relativeVelocityAtContact - normalRelativeVelocityAtContact;
		slidingSpring = integrateSlidingOrRollingSpring(slidingSpring, 
		slidingRelativeVelocity, 
		contactNormal, 
		normalContactForce, 
		slidingFrictionCoefficient, 
		slidingStiffness, 
		slidingDampingCoefficient, 
		timeStep);
		const double3 slidingForce = -slidingStiffness * slidingSpring - slidingDampingCoefficient * slidingRelativeVelocity;

		const double3 rollingRelativeVelocity = -effectiveRadius * cross(contactNormal, relativeAngularVelocityAtContact);
		rollingSpring = integrateSlidingOrRollingSpring(rollingSpring, 
		rollingRelativeVelocity, 
		contactNormal, 
		normalContactForce, 
		rollingFrictionCoefficient, 
		rollingStiffness, 
		rollingDampingCoefficient, 
		timeStep);
		const double3 rollingForce = -rollingStiffness * rollingSpring - rollingDampingCoefficient * rollingRelativeVelocity;
		const double3 rollingTorque = effectiveRadius * cross(contactNormal, rollingForce);

		const double effectiveDiameter = 2 * effectiveRadius;
		const double3 torsionRelativeVelocity = effectiveDiameter * dot(relativeAngularVelocityAtContact, contactNormal) * contactNormal;
		torsionSpring = integrateTorsionSpring(torsionSpring, 
		torsionRelativeVelocity, 
		contactNormal, 
		normalContactForce, 
		torsionFrictionCoefficient, 
		torsionStiffness, 
		torsionDampingCoefficient, 
		timeStep);
		const double3 torsionForce = -torsionStiffness * torsionSpring - torsionDampingCoefficient * torsionRelativeVelocity;
		const double3 torsionTorque = effectiveDiameter * torsionForce;

		contactForce = normalContactForce + slidingForce;
		contactTorque = rollingTorque + torsionTorque;
	}
	else
	{
		slidingSpring = make_double3(0., 0., 0.);
		rollingSpring = make_double3(0., 0., 0.);
		torsionSpring = make_double3(0., 0., 0.);
	}
}

__device__ __forceinline__ void HertzianMindlinContact(double3& contactForce, 
double3& contactTorque, 
double3& slidingSpring, 
double3& rollingSpring, 
double3& torsionSpring,
const double3 relativeVelocityAtContact,
const double3 relativeAngularVelocityAtContact,
const double3 contactNormal,
const double normalOverlap,
const double effectiveMass,
const double effectiveRadius,
const double timeStep,
const double dissipation,
const double effectiveElasticModulus,
const double effectiveShearModulus,
const double stiffnessRatioRollingToSliding,
const double stiffnessRatioTorsionToSliding,
const double slidingFrictionCoefficient,
const double rollingFrictionCoefficient,
const double torsionFrictionCoefficient)
{
	if (normalOverlap > 0)
	{
		const double normalStiffness = 4. / 3. * effectiveElasticModulus * sqrt(effectiveRadius * normalOverlap);
		const double slidingStiffness = 8. * effectiveShearModulus * sqrt(effectiveRadius * normalOverlap);
		const double normalDampingCoefficient = 2. * sqrt(5. / 6.) * dissipation * sqrt(effectiveMass * normalStiffness);
		const double slidingDampingCoefficient = 2. * sqrt(5. / 6.) * dissipation * sqrt(effectiveMass * slidingStiffness);

		const double rollingStiffness = slidingStiffness * stiffnessRatioRollingToSliding;
		const double torsionStiffness = slidingStiffness * stiffnessRatioTorsionToSliding;
		const double rollingDampingCoefficient = 2. * sqrt(5. / 6.) * dissipation * sqrt(effectiveMass * rollingStiffness);
		const double torsionDampingCoefficient = 2. * sqrt(5. / 6.) * dissipation * sqrt(effectiveMass * torsionStiffness);

		const double3 normalRelativeVelocityAtContact = dot(relativeVelocityAtContact, contactNormal) * contactNormal;
		const double3 normalContactForce = normalStiffness * normalOverlap * contactNormal - normalRelativeVelocityAtContact * normalDampingCoefficient;

		const double3 slidingRelativeVelocity = relativeVelocityAtContact - normalRelativeVelocityAtContact;
		slidingSpring = integrateSlidingOrRollingSpring(slidingSpring, 
		slidingRelativeVelocity, 
		contactNormal, 
		normalContactForce, 
		slidingFrictionCoefficient, 
		slidingStiffness, 
		slidingDampingCoefficient, 
		timeStep);
		const double3 slidingForce = -slidingStiffness * slidingSpring - slidingDampingCoefficient * slidingRelativeVelocity;

		const double3 rollingRelativeVelocity = -effectiveRadius * cross(contactNormal, relativeAngularVelocityAtContact);
		rollingSpring = integrateSlidingOrRollingSpring(rollingSpring, 
		rollingRelativeVelocity, 
		contactNormal, 
		normalContactForce, 
		rollingFrictionCoefficient, 
		rollingStiffness, 
		rollingDampingCoefficient, 
		timeStep);
		const double3 rollingForce = -rollingStiffness * rollingSpring - rollingDampingCoefficient * rollingRelativeVelocity;
		const double3 rollingTorque = effectiveRadius * cross(contactNormal, rollingForce);

		const double effectiveDiameter = 2 * effectiveRadius;
		const double3 torsionRelativeVelocity = effectiveDiameter * dot(relativeAngularVelocityAtContact, contactNormal) * contactNormal;
		torsionSpring = integrateTorsionSpring(torsionSpring, 
		torsionRelativeVelocity, 
		contactNormal, 
		normalContactForce, 
		torsionFrictionCoefficient, 
		torsionStiffness, 
		torsionDampingCoefficient, 
		timeStep);
		const double3 torsionForce = -torsionStiffness * torsionSpring - torsionDampingCoefficient * torsionRelativeVelocity;
		const double3 torsionTorque = effectiveDiameter * torsionForce;

		contactForce = normalContactForce + slidingForce;
		contactTorque = rollingTorque + torsionTorque;
	}
	else
	{
		slidingSpring = make_double3(0., 0., 0.);
		rollingSpring = make_double3(0., 0., 0.);
		torsionSpring = make_double3(0., 0., 0.);
	}
}

enum class SphereTriangleContactType
{
    None,
    Face,
    Edge,
    Vertex
};

// ------------------------------------------------------------
// Small helpers
// ------------------------------------------------------------
__device__ __forceinline__ double clamp01(const double x)
{
    return (x < 0.0) ? 0.0 : ((x > 1.0) ? 1.0 : x);
}

__device__ __forceinline__ double dist2(const double3& a, const double3& b)
{
    const double3 d = a - b;
    return dot(d, d);
}

// Closest point on segment [a,b] to p. Returns q and outputs t in [0,1].
__device__ __forceinline__
double3 closestPointOnSegment(const double3& a,
                              const double3& b,
                              const double3& p,
                              double& tOut)
{
    const double3 ab  = b - a;
    const double  ab2 = dot(ab, ab);
    if (ab2 <= 1e-30)
    {
        tOut = 0.0;
        return a;
    }
    tOut = clamp01(dot(p - a, ab) / ab2);
    return a + ab * tOut;
}

// ------------------------------------------------------------
// Edge-contact test (strictly edge interior, not vertices)
// ------------------------------------------------------------
__device__ __forceinline__
bool isSphereEdgeContact(const double3& edgeP0,
                         const double3& edgeP1,
                         const double3& sphereCenter,
                         const double   sphereRadius)
{
    const double3 e   = edgeP1 - edgeP0;
    const double  e2  = dot(e, e);
    if (e2 <= 1e-30) return false;

    const double3 v   = sphereCenter - edgeP0;
    const double  t   = dot(v, e) / e2;

    // Interior only: exclude endpoints (t<=0 or t>=1 => vertex contact)
    // Use a tiny margin to avoid numerical flicker near vertices.
    const double tEps = 1e-14;
    if (t <= tEps || t >= 1.0 - tEps) return false;

    const double3 q   = edgeP0 + e * t;
    const double  r2  = sphereRadius * sphereRadius;

    // Scale-aware epsilon on distance^2
    const double eps2 = 1e-12 * fmax(1.0, r2);

    return dist2(sphereCenter, q) <= r2 + eps2;
}

// ------------------------------------------------------------
// Main classifier: returns contact type and closestPoint.
// - Uses Ericson region tests for non-degenerate triangle.
// - Degenerate triangle handled as 3 segments.
// ------------------------------------------------------------
__device__ __forceinline__
SphereTriangleContactType classifySphereTriangleContact(const double3& sphereCenter,
                                                        const double  sphereRadius,
                                                        const double3& v0,
                                                        const double3& v1,
                                                        const double3& v2,
                                                        double3& closestPoint)
{
    const double r2   = sphereRadius * sphereRadius;
    const double eps2 = 1e-12 * fmax(1.0, r2);

    const double3 ab = v1 - v0;
    const double3 ac = v2 - v0;

    const double3 n     = cross(ab, ac);
    const double  area2 = dot(n, n);

    // --------------------------------------------------------
    // Degenerate triangle -> treat as 3 segments
    // --------------------------------------------------------
    if (area2 <= 1e-20)
    {
        double t01, t02, t12;
        const double3 q01 = closestPointOnSegment(v0, v1, sphereCenter, t01);
        const double3 q02 = closestPointOnSegment(v0, v2, sphereCenter, t02);
        const double3 q12 = closestPointOnSegment(v1, v2, sphereCenter, t12);

        double dmin = dist2(sphereCenter, q01);
        closestPoint = q01;
        SphereTriangleContactType type =
            (t01 > 0.0 && t01 < 1.0) ? SphereTriangleContactType::Edge
                                     : SphereTriangleContactType::Vertex;

        const double d02 = dist2(sphereCenter, q02);
        if (d02 < dmin)
        {
            dmin = d02;
            closestPoint = q02;
            type = (t02 > 0.0 && t02 < 1.0) ? SphereTriangleContactType::Edge
                                           : SphereTriangleContactType::Vertex;
        }

        const double d12 = dist2(sphereCenter, q12);
        if (d12 < dmin)
        {
            dmin = d12;
            closestPoint = q12;
            type = (t12 > 0.0 && t12 < 1.0) ? SphereTriangleContactType::Edge
                                           : SphereTriangleContactType::Vertex;
        }

        return (dmin <= r2 + eps2) ? type : SphereTriangleContactType::None;
    }

    // --------------------------------------------------------
    // Ericson region tests (non-degenerate triangle)
    // Naming follows "Real-Time Collision Detection" style:
    // d1 = ab·ap, d2 = ac·ap, etc.
    // --------------------------------------------------------
    const double3 ap = sphereCenter - v0;
    const double  d1 = dot(ab, ap);
    const double  d2 = dot(ac, ap);

    // Vertex region v0
    if (d1 <= 0.0 && d2 <= 0.0)
    {
        closestPoint = v0;
        return (dist2(sphereCenter, v0) <= r2 + eps2) ? SphereTriangleContactType::Vertex
                                                      : SphereTriangleContactType::None;
    }

    // Vertex region v1
    const double3 bp = sphereCenter - v1;
    const double  d3 = dot(ab, bp);
    const double  d4 = dot(ac, bp);
    if (d3 >= 0.0 && d4 <= d3)
    {
        closestPoint = v1;
        return (dist2(sphereCenter, v1) <= r2 + eps2) ? SphereTriangleContactType::Vertex
                                                      : SphereTriangleContactType::None;
    }

    // Edge region v0-v1
    const double vc = d1 * d4 - d3 * d2;
    if (vc <= 0.0 && d1 >= 0.0 && d3 <= 0.0)
    {
        const double t = d1 / (d1 - d3); // in [0,1]
        closestPoint = v0 + ab * t;
        return (dist2(sphereCenter, closestPoint) <= r2 + eps2) ? SphereTriangleContactType::Edge
                                                                : SphereTriangleContactType::None;
    }

    // Vertex region v2
    const double3 cp = sphereCenter - v2;
    const double  d5 = dot(ab, cp);
    const double  d6 = dot(ac, cp);
    if (d6 >= 0.0 && d5 <= d6)
    {
        closestPoint = v2;
        return (dist2(sphereCenter, v2) <= r2 + eps2) ? SphereTriangleContactType::Vertex
                                                      : SphereTriangleContactType::None;
    }

    // Edge region v0-v2
    const double vb = d5 * d2 - d1 * d6;
    if (vb <= 0.0 && d2 >= 0.0 && d6 <= 0.0)
    {
        const double t = d2 / (d2 - d6); // in [0,1]
        closestPoint = v0 + ac * t;
        return (dist2(sphereCenter, closestPoint) <= r2 + eps2) ? SphereTriangleContactType::Edge
                                                                : SphereTriangleContactType::None;
    }

    // Edge region v1-v2
    const double va = d3 * d6 - d5 * d4;
    if (va <= 0.0 && (d4 - d3) >= 0.0 && (d5 - d6) >= 0.0)
    {
        const double t = (d4 - d3) / ((d4 - d3) + (d5 - d6)); // in [0,1]
        closestPoint = v1 + (v2 - v1) * t;
        return (dist2(sphereCenter, closestPoint) <= r2 + eps2) ? SphereTriangleContactType::Edge
                                                                : SphereTriangleContactType::None;
    }

    // Face region
    const double sum = va + vb + vc;
    if (fabs(sum) <= 1e-30) return SphereTriangleContactType::None;

    const double invSum = 1.0 / sum;
    const double v = vb * invSum;
    const double w = vc * invSum;
    closestPoint = v0 + ab * v + ac * w;

    return (dist2(sphereCenter, closestPoint) <= r2 + eps2) ? SphereTriangleContactType::Face
                                                            : SphereTriangleContactType::None;
}

extern "C" void luanchCalculateBallContactForceTorque(double3* position, 
double3* velocity, 
double3* angularVelocity, 
double* radius,
double* inverseMass,
int* materialID,

double3* slidingSpring, 
double3* rollingSpring, 
double3* torsionSpring, 
double3* contactForce,
double3* contactTorque,
double3* contactPoint,
double3* contactNormal,
double* overlap,
int* objectPointed, 
int* objectPointing,

const double timeStep,

const size_t numInteractions,
const size_t gridD,
const size_t blockD, 
cudaStream_t stream);

extern "C" void luanchCalculateBondedForceTorque(double3* position, 
double3* velocity, 
double3* angularVelocity, 
double3* force, 
double3* torque,
double* radius,
int* materialID,
int* neighborPrefixSum,

double3* contactForce,
double3* contactTorque,
double3* contactPoint,
double3* contactNormal,
int* objectPointing, 

double3* bondPoint,
double3* bondNormal,
double3* shearForce, 
double3* bendingTorque,
double* normalForce, 
double* torsionTorque, 
double* maxNormalStress,
double* maxShearStress,
int* isBonded, 
int* objectPointed_b, 
int* objectPointing_b,

const double timeStep,

const size_t numBondedInteraction,
const size_t gridD,
const size_t blockD, 
cudaStream_t stream);

extern "C" void luanchSumBallContactForceTorque(double3* position, 
double3* force, 
double3* torque,
int* neighborPrefixSum,
int* interactionStart, 
int* interactionEnd,

double3* contactForce,
double3* contactTorque,
double3* contactPoint,
int* interactionHashIndex,

const size_t numBall,
const size_t gridD,
const size_t blockD, 
cudaStream_t stream);

extern "C" void luanchCalculateBallWallContactForceTorque(double3* position, 
double3* velocity, 
double3* angularVelocity, 
double3* force, 
double3* torque,
double* radius,
double* inverseMass,
int* materialID,
int* neighborPrefixSum,

double3* position_w, 
double3* velocity_w, 
double3* angularVelocity_w, 
int* materialID_w,

int* index0_t, 
int* index1_t, 
int* index2_t, 
int* wallIndex_tri,

double3* globalPosition_v, 

double3* slidingSpring, 
double3* rollingSpring, 
double3* torsionSpring,
double3* contactForce,
double3* contactTorque,
double3* contactPoint,
double3* contactNormal,
double* overlap,
int* objectPointed, 
int* objectPointing,
int* cancelFlag,

const double timeStep,

const size_t numBall,
const size_t gridD,
const size_t blockD, 
cudaStream_t stream);